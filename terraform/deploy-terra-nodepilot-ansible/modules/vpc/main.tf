# 1- Define the VPC 
resource "aws_vpc" "depi-project" {
  region     = var.aws_region
  cidr_block = var.vpc_cidr

  tags = {
    Name        = var.vpc_name
    Environment = "Project Environment"
    Terraform   = "true"
  }
}


#2- Deploy the public subnets

resource "aws_subnet" "public_subnets" {
  for_each                = var.public_subnets
  vpc_id                  = aws_vpc.depi-project.id
  cidr_block              = cidrsubnet(var.vpc_cidr, 8, each.value)
  map_public_ip_on_launch = true
  availability_zone       = each.key
  tags = {
    Name      = "${each.key}_public_subnet"
    Terraform = "true"
  }
}




#3- Deploy the private subnets
resource "aws_subnet" "private_subnets" {
  for_each          = var.private_subnets
  vpc_id            = aws_vpc.depi-project.id
  cidr_block        = cidrsubnet(var.vpc_cidr, 8, each.value)
  availability_zone = each.key

  tags = {
    Name      = "${each.key}_private_subnet"
    Terraform = "true"
  }
}


# 4- Create Internet Gateway
resource "aws_internet_gateway" "internet_gateway" {
  vpc_id = aws_vpc.depi-project.id
  tags = {
    Name = "project_igw"
  }

}


# 5- Create EIP for NAT Gateway
resource "aws_eip" "nat_gateway_eip" {
  domain = "vpc"
  #This dependency is for ordering but technically its not needed
  depends_on = [aws_internet_gateway.internet_gateway]
  tags = {
    Name = "project_igw_eip"
  }

}

# 6- Create NAT Gateway
resource "aws_nat_gateway" "nat_gateway" {
  #A NAT Gateway itself needs to reach the internet.
  #Private subnet → NAT Gateway → Internet Gateway → Internet
  depends_on    = [aws_subnet.public_subnets]
  allocation_id = aws_eip.nat_gateway_eip.id
  subnet_id     = aws_subnet.public_subnets["us-east-1a"].id
  tags = {
    Name = "project_nat_gateway"
  }
}

# 7- Create route tables for private subnets
resource "aws_route_table" "private_route_table" {
  vpc_id = aws_vpc.depi-project.id

  route {
    cidr_block     = "0.0.0.0/0"
    nat_gateway_id = aws_nat_gateway.nat_gateway.id
  }
  tags = {
    Name      = "project_private_rtb"
    Terraform = "true"
  }
}
# 8- Create route table associatioin for Private subnets.

resource "aws_route_table_association" "private" {
  depends_on     = [aws_subnet.private_subnets]
  route_table_id = aws_route_table.private_route_table.id
  for_each       = aws_subnet.private_subnets
  subnet_id      = each.value.id
}

# 9- Create route table for public subnets
resource "aws_route_table" "public_route_table" {
  vpc_id = aws_vpc.depi-project.id

  route {
    cidr_block = "0.0.0.0/0"
    gateway_id = aws_internet_gateway.internet_gateway.id
  }
  tags = {
    Name      = "project_public_rtb"
    Terraform = "true"
  }
}
#10- Create route table associations for Public subnets
resource "aws_route_table_association" "public" {
  depends_on     = [aws_subnet.public_subnets]
  route_table_id = aws_route_table.public_route_table.id
  for_each       = aws_subnet.public_subnets
  subnet_id      = each.value.id
}



## Diable Overwrite for this CIDAR
# locals {
#  
# }

